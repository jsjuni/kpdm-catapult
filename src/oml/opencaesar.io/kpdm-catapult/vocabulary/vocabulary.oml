@dc:title "KPDM Catapult Vocabulary"
@dc:creator "J S Jenkins"
@rdfs:label "vocabulary"
vocabulary <http://opencaesar.io/kpdm-catapult/vocabulary/vocabulary#> as kpdm-vocabulary-vocabulary {
	
	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
	extends <http://purl.org/dc/elements/1.1/> as dc
	
	extends <http://bipm.org/jcgm/vim4#> as vim4
	
	extends <http://imce.jpl.nasa.gov/foundation/base#> as base
	extends <http://imce.jpl.nasa.gov/foundation/mission#> as mission
	
	uses <http://iso.org/iso-80000-3.1#> as iso-80000-3.1
	uses <http://iso.org/iso-80000-3.3#> as iso-80000-3.3
	uses <http://iso.org/iso-80000-4.1#> as iso-80000-4.1
	uses <http://iso.org/iso-80000-4.7#> as iso-80000-4.7
	
	concept Catapult < mission:Component [
		restricts some base:contains to Arm
	]
	
	concept Arm < mission:Component
	
	concept Ball < mission:Component
	
	concept LengthQuantity < vim4:InherentUnitaryQuantity, base:IdentifiedElement [
		restricts vim4:instantiates to iso-80000-3.1:length
	]
	
	concept AreaQuantity < vim4:InherentUnitaryQuantity, base:IdentifiedElement [
		restricts vim4:instantiates to iso-80000-3.3:area
	]
	
	concept MassQuantity < vim4:InherentUnitaryQuantity, base:IdentifiedElement [
		restricts vim4:instantiates to iso-80000-4.1:mass
	]
	
	concept MomentOfInertiaQuantity < vim4:InherentUnitaryQuantity, base:IdentifiedElement [
		restricts vim4:instantiates to iso-80000-4.7:moment_of_inertia
	]
	
	concept MetreValue < vim4:UnitaryQuantityValue, base:IdentifiedElement [
		restricts all vim4:isAttributedTo to LengthQuantity
		restricts vim4:unit to iso-80000-3.1:metre
	]
	
	concept SquareMetreValue < vim4:UnitaryQuantityValue, base:IdentifiedElement [
		restricts all vim4:isAttributedTo to AreaQuantity
		restricts vim4:unit to iso-80000-3.3:square_metre
	]
	
	concept KilogramValue < vim4:UnitaryQuantityValue, base:IdentifiedElement [
		restricts all vim4:isAttributedTo to MassQuantity
		restricts vim4:unit to iso-80000-4.1:kilogram
	]
	
	concept KilogramMetreSquaredValue < vim4:UnitaryQuantityValue, base:IdentifiedElement [
		restricts all vim4:isAttributedTo to MomentOfInertiaQuantity
		restricts vim4:unit to iso-80000-4.7:kilogram-metre-squared
	]
	
}
